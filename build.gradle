plugins {
    id 'application'
    // Apply the JavaFX gradle plugin:
    id 'org.openjfx.javafxplugin' version '0.0.13'
    // Badass JLink Plugin
    id "org.beryx.jlink" version "2.25.0"
    // Plugin for code coverage
    id 'jacoco'
}

application {
    mainClass = "es.upm.fi.cig.multictbnc.Main"
    mainModule = "es.upm.fi.cig.multictbnc"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

test {
    useJUnitPlatform()
}

sourceSets {
    test {
        resources {
            exclude '*'
        }
    }
}

dependencies {
    // Google Core Libraries for Java
    implementation 'com.google.guava:guava:28.2-jre'
    // GraphStream library. It is used to display graphs
    implementation 'com.github.graphstream:gs-core:2.0'
    implementation group: 'com.github.graphstream', name: 'gs-ui-javafx', version: '2.0', {
        exclude group: 'org.openjfx'
    }
    // LOG4j logging library
    implementation 'org.apache.logging.log4j:log4j-api:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    // OpenCSV
    implementation 'com.opencsv:opencsv:3.7'
    // Extract controls for JavaFX
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.2', {
        exclude group: 'org.openjfx'
    }
    // The Apache Commons Math project
    implementation 'org.apache.commons:commons-math3:3.6.1'
    // Apache POI
    implementation 'org.apache.poi:poi-ooxml:5.2.2'
    // JFreeChart
    implementation 'org.jfree:jfreechart:1.5.0'
    // JFreeChart-FX
    implementation 'org.jfree:jfreechart-fx:1.0.1'
    // FXGraphics2D
    implementation 'org.jfree:fxgraphics2d:1.8'
    // Use the JUnit test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
    implementation 'org.jfxtras:jmetro:11.6.16'
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xmx31744m']
}

test {
    maxHeapSize = "30720m"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

jlink {
    options = ["--strip-debug", "--compress", "1", "--no-header-files", "--no-man-pages"]
    forceMerge('log4j-api')
    addExtraDependencies("javafx")
    launcher {
        name = 'Multi-CTBNCs'
    }
    jpackage {
        installerOptions += [
                '--description', project.description,
                '--app-version', version,
                '--license-file', 'LICENSE',
                '--copyright', 'Copyrigtht 2022 Carlos Villa Blanco',
                '--vendor', 'Carlos Villa Blanco'
        ]
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions = ['--win-console']
            skipInstaller = true
            icon = 'imgs/icon.ico'
        } else if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            icon = 'imgs/icon.icns'
            installerType = "pkg"
        } else if (org.gradle.internal.os.OperatingSystem.current().linux) {
            icon = 'imgs/icon.png'
            installerOptions += ['--linux-menu-group', 'Science', '--linux-shortcut', '--linux-deb-maintainer', 'carlos.villa@upm.es']
        }
    }
}

// ------------ TASKS FOR THE ARTICLE "MULTIDIMENSIONAL CONTINUOUS TIME BAYESIAN NETWORK CLASSIFIERS" ------------ 

task runAllExperimentsVillaBlancoEtAl2021(type: GradleBuild) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs all the experiments with the synthetic and energy datasets from Villa-Blanco et al. (2021)'
    tasks = ['syntheticLL', 'syntheticBDe', 'emptyDigraphMultiCTBNCLL', 'emptyDigraphMultiCTBNCBDe', 'syntheticCLL', 'emptyDigraphMultiCTBNCCLL', 'energyBIC', 'energyBDe']
}

task syntheticLL(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs synthetic experiments to compare the performance of CTBNCs and a Multi-CTBNC when they are learnt with the log-likpenalisedcore penalised with BIC'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'synthetic', 'CTBNCs', 'Multi-CTBNC', 'Log-likelihood', 'BIC'
}

task syntheticCLL(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs synthetic experiments to compare the performance of CTBNCs and a Multi-CTBNC when they are learnt with the conditional log-likpenalisedcore penalised with BIC'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'synthetic', 'CTBNCs', 'Multi-CTBNC', 'Conditional log-likelihood', 'BIC'
}

task syntheticBDe(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs synthetic experiments to compare the performance of CTBNCs and a Multi-CTBNC when they are learnt with the Bayesian Dirichlet equivalent score'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'synthetic', 'CTBNCs', 'Multi-CTBNC', 'Bayesian Dirichlet equivalent', 'No'
}

task energyBIC(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs experiments with the energy dataset to compare the performance of max1 CTBNCs and a DAG-max1 Multi-CTBNC when they are learnt with the BIC-penalised log-likelihood score'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'energy', 'maxK CTBNCs', 'DAG-maxK Multi-CTBNC', 'Log-likelihood', 'BIC'
}

task energyBDe(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs experiments with the energy dataset to compare the performance of max1 CTBNCs and a DAG-max1 Multi-CTBNC when they are learnt with the Bayesian Dirichlet equivalent score'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'energy', 'maxK CTBNCs', 'DAG-maxK Multi-CTBNC', 'Bayesian Dirichlet equivalent', 'No'
}

task emptyDigraphMultiCTBNCBDe(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs synthetic experiments to compare the performance of CTBNCs and an empty-digraph Multi-CTBNC when they are learnt with the Bayesian Dirichlet equivalent score'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'synthetic', 'CTBNCs', 'Empty-digraph Multi-CTBNC', 'Bayesian Dirichlet equivalent', 'No'
}

task emptyDigraphMultiCTBNCLL(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs synthetic experiments to compare the performance of CTBNCs and an empty-digraph Multi-CTBNC when they are learnt with the Bayesian Dirichlet equivalent score'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'synthetic', 'CTBNCs', 'Empty-digraph Multi-CTBNC', 'Log-likelihood', 'BIC'
}

task emptyDigraphMultiCTBNCCLL(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2021'
    description 'Runs synthetic experiments to compare the performance of CTBNCs and an empty-digraph Multi-CTBNC when they are learnt with the Bayesian Dirichlet equivalent score'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsMultiCTBNC')
    args 'synthetic', 'CTBNCs', 'Empty-digraph Multi-CTBNC', 'Conditional log-likelihood', 'BIC'
}

// ------------ TASKS FOR THE ARTICLE "CONSTRAINT-BASED AND HYBRID STRUCTURE LEARNING OF MULTIDIMENSIONAL CONTINUOUS-TIME BAYESIAN NETWORK CLASSIFIERS" ------------

task britishHouseholdPanelSurvey(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Experiments to evaluate the performance of the structure learning algorithms on a real-world dataset'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsStructureLearning')
    args 'datasets/VillaBlancoEtAl2023/section_5_3_british_household_panel_survey', 'Hill climbing (BIC);Tabu search (BIC);Hill climbing (BDe);CTPC;MB-CTPC;Hybrid algorithm [size sep set = 2]', 'Dental check-up;Employment status;Limb, back or neck problems;Lives with spouse or partner;Responsible adult for child;Sex;Smoker'
}

task highDimensionality(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Experiments to evaluate the difference in learning time of the algorithms when the dimensionality of the datasets increase significantly'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsStructureLearning')
    args 'datasets/VillaBlancoEtAl2023/section_5_1_3_high_dimensionality', 'CTPC;MB-CTPC'
}

task increaseFeatureStates10Duration(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Experiments to evaluate the influence of the cardinality of feature variables on the performance of algorithms when sequences have a duration of 10 time units'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsStructureLearning')
    args 'datasets/VillaBlancoEtAl2023/section_5_1_2_increase_feature_states_10_duration'
}

task increaseFeatureStates20Duration(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Experiments to evaluate the influence of the cardinality of feature variables on the performance of algorithms when sequences have a duration of 20 time units'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsStructureLearning')
    args 'datasets/VillaBlancoEtAl2023/section_5_1_2_increase_feature_states_20_duration'
}

task mainExperiments(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Comprehensive experiments over randomly generated datasets using different number of class and feature variables, cardinalities and density of the structures'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsStructureLearning')
    args 'datasets/VillaBlancoEtAl2023/main_datasets'
}

task noisyData(type: JavaExec) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Experiments to evaluate the influence of noise on the performance of the structure learning algorithms'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.experiments.MainExperimentsStructureLearning')
    args 'datasets/VillaBlancoEtAl2023/section_5_2_6_noisy_data'
}

task runAllSyntheticExperimentsVillaBlancoEtAl2023(type: GradleBuild) {
    group 'Experiments Villa-Blanco et al. 2023'
    description 'Runs all the experiments from Villa-Blanco et al. (2023)'
    tasks = ['mainExperiments', 'increaseFeatureStates10Duration', 'increaseFeatureStates20Duration', 'highDimensionality', 'noisyData']
}

// ------------ SAMPLING DATASETS ------------

task sampleDatasetsVillaBlancoEtAl2021 {
    group 'Sampling'
    description 'Samples synthetic datasets as in Villa-Blanco et al. (2021)'
    doLast {
        exec {
            workingDir "${projectDir}"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine "${projectDir}/src/main/resources/samplingdatasets/sampleDatasetsVillaBlancoEtAl2021.bat"
            } else {
                executable 'sh'
                args "${projectDir}/src/main/resources/samplingdatasets/sampleDatasetsVillaBlancoEtAl2021.sh"
            }
        }
    }
}

task sampleDatasetsVillaBlancoEtAl2023 {
    group 'Sampling'
    description 'Samples synthetic datasets as in Villa-Blanco et al. (2023)'
    doLast {
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine "${projectDir}/src/main/resources/samplingdatasets/sampleDatasetsVillaBlancoEtAl2023.bat"
            } else {
                executable 'sh'
                args "${projectDir}/src/main/resources/samplingdatasets/sampleDatasetsVillaBlancoEtAl2023.sh"
            }
        }
    }
}

task sampleDatasets(type: JavaExec) {
    group 'Sampling'
    description 'Samples discrete state multi-variate time series datasets with multiple class variables'
    classpath sourceSets.main.runtimeClasspath
    mainClass.set('es.upm.fi.cig.multictbnc.sampling.MainSampling')
}